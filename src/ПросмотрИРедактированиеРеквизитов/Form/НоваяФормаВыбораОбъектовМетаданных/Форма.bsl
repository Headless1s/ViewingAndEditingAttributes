
#Область ОбработчикиСобытийЭлементовТаблицыФормы

&НаКлиенте
Процедура СписокОбъектовМетаданныхВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОповеститьОВыбореОбъектаМетаданных();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура МетаданныеВыбор(Команда)
	
	ВыбранныеМетанные = СтрЗаменить(Команда.Имя, "Метаданные", "");
	
    ЗаполнитьСписокМетаданных(ВыбранныеМетанные);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	ОповеститьОВыбореОбъектаМетаданных();		
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокМетаданных(ВыбранныеМетанные)

	СписокОбъектовМетаданных.Очистить();
	
	Картинка = Неопределено;
		
	Для каждого ОбъектМетаданных Из Метаданные[ВыбранныеМетанные] Цикл
		
		НоваяСтрока = СписокОбъектовМетаданных.Добавить();
		НоваяСтрока.Имя = ОбъектМетаданных.Имя;
		НоваяСтрока.ПолноеИмя = ОбъектМетаданных.ПолноеИмя();
		
		Если ЗначениеЗаполнено(ОбъектМетаданных.Синоним) Тогда
			НоваяСтрока.Синоним = ОбъектМетаданных.Синоним;
		Иначе
			НоваяСтрока.Синоним = ОбъектМетаданных.Имя;	
		КонецЕсли;
				
		Если Картинка = Неопределено Тогда
			ИмяКартинки = СтрРазделить(НоваяСтрока.ПолноеИмя, ".")[0];
			Картинка = БиблиотекаКартинок[ИмяКартинки];
		КонецЕсли;
		 	
		НоваяСтрока.Картинка = Картинка;
		
	КонецЦикла;
	
	СписокОбъектовМетаданных.Сортировать("Синоним ВОЗР");
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьОВыбореОбъектаМетаданных()
	
	ТекущаяСтрока = Элементы.СписокОбъектовМетаданных.ТекущиеДанные;
	
	Если ТекущаяСтрока <> Неопределено Тогда
		
		Оповестить("ВыборОбъектовМетаданных", ТекущаяСтрока.ПолноеИмя, ЭтотОбъект); 
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


