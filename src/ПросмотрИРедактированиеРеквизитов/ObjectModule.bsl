
#Область ПрограммныйИнтерфейс

Функция РеквизитТаблицаЗначений(Имя) Экспорт
	Возврат Новый РеквизитФормы(Имя, Новый ОписаниеТипов("ТаблицаЗначений"));		
КонецФункции

Функция РеквизитДинамическийСписок(Имя) Экспорт
	Возврат Новый РеквизитФормы(Имя, Новый ОписаниеТипов("ДинамическийСписок"));	
КонецФункции

Функция ПолучитьМассивПолейТаблицы(ТаблицаФормы, СписокПолейКроме = Неопределено) Экспорт
	
	МассивПолей = Новый Массив;
	
	ЗаполнитьМассивПолейРекурсивно(ТаблицаФормы.ПодчиненныеЭлементы, СписокПолейКроме, МассивПолей);
		
	Возврат МассивПолей;
	
КонецФункции

#КонецОбласти 

#Область УстановкаУсловногоОформления

Процедура ЗаполнитьМассивПолейРекурсивно(Коллекция, СписокПолейКроме, МассивПолей)
			
	Для каждого ЭлементТаблицы Из Коллекция Цикл	
		Если ТипЗнч(ЭлементТаблицы) = Тип("ГруппаФормы") Тогда
			ЗаполнитьМассивПолейРекурсивно(ЭлементТаблицы.ПодчиненныеЭлементы, СписокПолейКроме, МассивПолей);	
		Иначе
			Если СписокПолейКроме <> Неопределено Тогда
				Если СписокПолейКроме.Найти(ЭлементТаблицы.Имя) <> Неопределено Тогда
					Продолжить;
				КонецЕсли; 
			КонецЕсли;
			МассивПолей.Добавить(ЭлементТаблицы.Имя);	
		КонецЕсли;
	КонецЦикла;	
				
КонецПроцедуры
 
Процедура ДобавитьЭлементУсловногоОформления(Форма, ПутиЭлементовОтбора, Сравнение, Значение, ПутиПолейКомпоновки, ИмяПараметра, ЗначениеПараметра) Экспорт
		
	ЭлементУсловногоОформления = Форма.УсловноеОформление.Элементы.Добавить();
	
	Если ТипЗнч(ПутиЭлементовОтбора) = Тип("ТаблицаЗначений") Тогда
		ЭлементГруппаОтбораУсловногоОформления = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
		ЭлементГруппаОтбораУсловногоОформления.ТипГруппы = Сравнение;
		Для каждого СтрокаПути Из ПутиЭлементовОтбора Цикл
			ЭлементОтбораУсловногоОформления = ЭлементГруппаОтбораУсловногоОформления.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЭлементОтбораУсловногоОформления.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(СтрокаПути.Путь);
			ЭлементОтбораУсловногоОформления.ВидСравнения = СтрокаПути.Сравнение;
			ЭлементОтбораУсловногоОформления.ПравоеЗначение = СтрокаПути.Значение;	
		КонецЦикла;
	Иначе
		ЭлементОтбораУсловногоОформления = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));		
		ЭлементОтбораУсловногоОформления.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутиЭлементовОтбора);;
		ЭлементОтбораУсловногоОформления.ВидСравнения = Сравнение;
		ЭлементОтбораУсловногоОформления.ПравоеЗначение = Значение;
	КонецЕсли; 	
	
	Если ТипЗнч(ПутиПолейКомпоновки) = Тип("Массив") Тогда
		Для каждого ПутьПолеКомпоновки Из ПутиПолейКомпоновки Цикл
			ПолеУсловногоОформления = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
			ПолеУсловногоОформления.Поле = Новый ПолеКомпоновкиДанных(ПутьПолеКомпоновки);
		КонецЦикла;
	Иначе
		ПолеУсловногоОформления = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		ПолеУсловногоОформления.Поле = Новый ПолеКомпоновкиДанных(ПутиПолейКомпоновки);
	КонецЕсли; 	 
		
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра(ИмяПараметра, ЗначениеПараметра);
	ЭлементУсловногоОформления.Использование = Истина;	
	
КонецПроцедуры

Функция ПолучитьТаблицуЭлементовОтбора() Экспорт
	
	ТаблицаЭлементовОтбора = Новый ТаблицаЗначений;
	ТаблицаЭлементовОтбора.Колонки.Добавить("Путь");
	ТаблицаЭлементовОтбора.Колонки.Добавить("Сравнение");
	ТаблицаЭлементовОтбора.Колонки.Добавить("Значение");	
	
	Возврат ТаблицаЭлементовОтбора;
	
КонецФункции 

#КонецОбласти 

